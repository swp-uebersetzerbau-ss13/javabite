task buildCompiler(type: Copy, dependsOn: ['clean',':javabite-compiler:fatJar']) {
	from 'javabite-compiler/build/libs'
	into 'bin'
	include 'javabite-compiler-*.*.jar'
} 
allprojects {
	apply plugin: 'java'
	apply plugin: 'eclipse'


	sourceCompatibility = 1.7
	version = '0.1'
	ext.title = 'Javabite'
	jar {
		manifest {
			attributes 'Implementation-Title': title, 'Implementation-Version': version
		}
	}

	repositories { mavenCentral() }

	dependencies {
		compile 'org.apache.commons:commons-io:1.3.2'
		compile 'org.slf4j:slf4j-simple:1.7.5'
		compile 'org.tinyjee.jgraphx:jgraphx:1.10.4.1'
		testCompile 'junit:junit:4.11'
		testCompile 'org.mockito:mockito-all:1.8.4'
	}

	test { systemProperties 'org.slf4j.simpleLogger.defaultLogLevel': 'debug' }
}
project(':common'){
	sourceSets {
		main {
			java { srcDir 'interfaces/src' }
		}
		test {
			java { srcDir 'interfaces/test' }
		}
	}
}
project(':javabite-compiler') {
	
	apply plugin:'application'
	apply plugin:'fatjar'

	jar {
		manifest {
			attributes 'Main-Class': 'swp_compiler_ss13.javabite.compiler.JavabiteCompiler'
		}
	}
	
	fatJar {
		manifest {
			attributes 'Main-Class': 'swp_compiler_ss13.javabite.compiler.JavabiteCompiler'
		}
	}
	
	mainClassName = 'swp_compiler_ss13.javabite.compiler.JavabiteCompiler'

	dependencies {
		compile project(':javabite-common'), project(':javabite-lexer'), project(':javabite-parser'), project(':javabite-code-gen'), project(':javabite-backend')
	}
}
project(':javabite-lexer') {
	dependencies { 
		compile project(':javabite-common') 
		testCompile project(':common').sourceSets.test.output
	}
}
project(':javabite-parser') {
	dependencies { compile project(':javabite-common') }
}
project(':javabite-code-gen') {
	dependencies { compile project(':javabite-common') }
}
project(':javabite-backend') {
	dependencies { compile project(':javabite-common') }
}
project(':javabite-common') {
	dependencies { compile project(':common') }
}
buildscript {
	repositories {
		mavenCentral()
	}

	dependencies {
		classpath 'eu.appsatori:gradle-fatjar-plugin:0.2-rc1'
	}
}
task wrapper(type: Wrapper) {
	gradleVersion = '1.6'
}